def versionMajor = 0 // incompatible API changes
def versionMinor = 6 // added functionality in a backwards-compatible manner
def versionPatch = 0 // backwards-compatible bug fixes
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo1.maven.org/maven2' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.1'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://android-rome-feed-reader.googlecode.com/svn/maven2/releases' }
}

apply plugin: 'android'
apply plugin: 'android-apt'

configurations {
    apt
}


def getSourceSetName(variant) {
    return new File(variant.dirName).getName();
}


android {
    compileSdkVersion 19
    buildToolsVersion "19.0.0"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        // prevents Error: duplicate files during packaging of APK
        // no wildcard support yet
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
    }

    // This is important, it will run lint checks but won't abort build
    lintOptions {
        abortOnError false
    }


    defaultConfig {
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        minSdkVersion 8
        targetSdkVersion 19

        packageName "com.github.ironjan.ccd"
    }


    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
        }
    }

    if (project.hasProperty("ccd.signing") && new File(project.property("ccd.signing") + ".gradle").exists()) {
        apply from: project.property("ccd.signing") + ".gradle";
    }
}

dependencies {
    compile 'com.android.support:support-v4:19.0.0'
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    apt "org.androidannotations:androidannotations:3.0"
    compile "org.androidannotations:androidannotations-api:3.0"
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}
